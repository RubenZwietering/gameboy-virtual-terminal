include "hardware.inc"
include "macros.inc"
include "debug.inc"
include "cursor.inc"

export frame_count

export init_vblank_mem

section "vblank variables", wramx
frame_count:				ds 1
in_vblank_handler:			ds 1

section "vblank interrupt", ROM0[INT_HANDLER_VBLANK] ; max 8 bytes
ei ; allow nested interrups so serial communication can still happen without data corruption
jp vblank_handler

section "vblank functions", ROMX
; init_vblank_mem
; 
; destroys `af`
init_vblank_mem:
	xor a
	ld [frame_count], a
	ld [in_vblank_handler], a
	ret
; init_vblank_mem


; vblank_handler
vblank_handler:
	PUSH_ALL

	ld a, [in_vblank_handler]
	or a ; cp 0
	jp nz, .return_vblank		; drop frame if already handling vblank

	inc a ; ld a, 1
	ld [in_vblank_handler], a
	
	call read_joypad
	call check_input

	ld hl, frame_count
	inc [hl]

	ld a, [cursor_blink_next_frame]
	cp [hl]
	push af

	; drawing
	;call wait_vblank

	ld hl, cursor_object
	ld a, [cursor_y]
	add a ; sla a
	add a ; sla a
	add a ; sla a
	ld b, OAM_Y_OFS
	add b
	ld [hli], a				; OAMA_Y

	ld a, [cursor_x]
	add a ; sla a
	add a ; sla a
	add a ; sla a
	ld b, OAM_X_OFS
	add b
	ld [hli], a				; OAMA_X

	pop af
	jr nz, .no_cursor_blink
		add CURSOR_BLINK_RATE
		ld [cursor_blink_next_frame], a

		ld a, [hl]
		xor CURSOR_CHARACTER
		ld [hl], a			; OAMA_TILEID
	.no_cursor_blink

	ld a, [character_window_offset]
	add a ; sla a
	add a ; sla a
	add a ; sla a
	ld [rSCY], a ; character_window_offset * 8

	ld bc, character_update_table_end

	.update_loop
		dec c
		jr z, .loop_end

		ld a, [bc]
		or a
		jr z, .update_loop

		ld a, c
		ld b, 0

		LD_R16_R16 hl, bc
		add hl, hl
		add hl, hl
		add hl, hl
		add hl, hl
		add hl, hl
		; hl = y * SCRN_VX_B

		ld de, _SCRN0 - SCRN_VX_B + SCRN_X_B
		add hl, de
		; hl = _SCRN0 + y * SCRN_VX_B
		
		LD_R16_R16 de, hl

		LD_R16_R16 hl, bc
		add hl, hl
		add hl, hl
		add hl, bc
		add hl, hl
		add hl, hl
		; hl = y * SCRN_X_B

		ld bc, character_buffer - 1
		add hl, bc
		; hl = character_buffer + y * SCRN_X_B

		ld c, a

	.do_update_fast
		ld b, SCRN_X_B
		.memcpy_loop
			ldd a, [hl]
			dec e 	; cannot go over a 256 byte boundary so only decreasing lower byte is fine
			ld [de], a
			dec b
			jr nz, .memcpy_loop

		ldh a, [rLY]
		DPUTS "LY=%A% Y=%C%"
		cp 144
		jr c, .exit_vblank

		xor a
		ld b, high(character_update_table)
		ld [bc], a

		dec c
		jr z, .loop_end

		ld a, [bc]
		or a
		jr z, .update_loop

		ld a, e
		sub SCRN_VX_B - SCRN_X_B
		ld e, a
		sbc a
		add d
		ld d, a
		jr .do_update_fast
	.loop_end

	if def(DEBUG)
	scf
	ccf
	endc
	.exit_vblank
	if def(DEBUG)
	jr nc, .no_dputs_1
		ld a, [frame_count]
		DPUTS "not done drawing #%A% CLK #%TOTALCLKS%"
	.no_dputs_1
	endc

	xor a
	ld [in_vblank_handler], a

	.return_vblank
	if def(DEBUG)
	jr z, .no_dputs
		ld a, [frame_count]
		DPUTS "dropped frame #%A% CLK #%TOTALCLKS%"
	.no_dputs
	endc

	POP_ALL
	ret ; reti
; vblank_handler
