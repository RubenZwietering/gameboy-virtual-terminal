include "macros.inc"

export _print_byte
export print_byte
export _print_binaryh
export print_binaryh
export _print_binary
export print_binary
export _print_hexadecimalh
export print_hexadecimalh
export _print_hexadecimal
export print_hexadecimal
export _print_hexadecimall
export print_hexadecimall
export _print_string
export print_string
export _vprintf
export vprintf
export _printf

def FORMATB_BEHAVIOUR_LEFT_JUSTIFIED 			equ 0
def FORMATB_BEHAVIOUR_SIGN_PREPENDED 			equ 1
def FORMATB_BEHAVIOUR_SPACE 					equ 2
def FORMATB_BEHAVIOUR_ALTERNATIVE_FORM 			equ 3
def FORMATB_BEHAVIOUR_LEADING_ZEROS 			equ 4
def FORMATB_HAS_MINIMUM_FIELD_WIDTH_SPECIFIER 	equ 5
def FORMATB_HAS_PRECISION_SPECIFIER 			equ 6

section "print functions", ROMX
; _print_byte
; 
; description:
; print a byte
; 
; c function signiature:
; int print_byte(unsigned char);
;
; input:
; - `a` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_byte:
	; fallthrough

; print_byte
;
; input:
; - `a` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_byte:
	ld c, $0f
	ld h, HIGH(hex_map)
	ld b, a

	and c
	ld l, a
	push hl

	ld a, b
	swap a
	and c
	ld l, a
	ld a, [hl]

	call putchar

	pop hl
	ld a, [hl]

	call putchar

	ld bc, 2
	ret
; print_byte
; _print_byte


; print_non_zero_nibbles
; 
; description:
; print byte without leading zero nibbles
;
; input:
; - `a` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_non_zero_nibbles:
	ld c, $0f
	ld h, HIGH(hex_map)
	ld b, a

	and c
	ld l, a
	push hl

	ld a, b
	swap a
	and c
	ld l, a
	ld a, [hl]

	call nz, putchar

	pop hl
	ld a, [hl]

	call putchar

	; TODO: return correct number of characters written
	ld bc, 1
	ret
; print_non_zero_nibbles


; _print_binaryh
; 
; description:
; print the bits of an 8 bit value with precision
; 
; c function signiature:
; int print_binaryh(unsigned char precision, unsigned char bits);
;
; input:
; - `a` : precision
; - `e` : bits
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_binaryh:
	; fallthrough

; print_binaryh
;
; input:
; - `a` : precision
; - `e` : bits
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_binaryh:
	cp 9
	jr nc, .extra_padding

	ld d, a
	ld a, 8
	sub d ; 8 - a
	jr z, .no_discard_zeros
		inc a
		.discard_leading_zeros_loop
			dec a
			jr z, .break_discard_leading_zeros_loop
			sla e
		jr nc, .discard_leading_zeros_loop
		rr e
		
		.break_discard_leading_zeros_loop
		
		add d
		ret z
		ld d, a
	.no_discard_zeros

	.print_bit_loop
		xor a
		sla e
		adc "0"

		push de
		call putchar
		pop de

		dec d
	jr nz, .print_bit_loop
	ret

	.extra_padding
		push de

		sub 8
		ld l, a
		ld h, "0" ; TODO: sign extend?

		.extra_padding_loop
			ld a, h
			push hl
			call putchar
			pop hl
			dec l
		jr nz, .extra_padding_loop

		pop de
		ld d, 8
		jr .print_bit_loop
; print_binaryh
; _print_binaryh


; _print_binary
; 
; description:
; print the bits of a 16 bit value with precision
; 
; c function signiature:
; int print_binary(unsigned char precision, unsigned int bits);
;
; input:
; - `a` : precision
; - `de` : bits
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_binary:
	; fallthrough

; print_binary
;
; input:
; - `a`  : precision TODO: 16 bit
; - `de` : bits
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_binary:
	cp 17
	jr c, .no_extra_padding
		push de
		sub 16
		ld l, a
		ld h, "0" ; TODO: sign extend?

		.extra_padding_loop
			push hl
			ld a, h
			call putchar
			pop hl
			dec l
		jr nz, .extra_padding_loop

		ld a, 16
		pop de
	.no_extra_padding

	ld l, d
	ld d, a
	push de
	ld e, l
	
	sub 8 ; jump if a > 8
	jr nc, .bigger_than_8
		xor a ; ld a, 0
	.bigger_than_8

	call print_binaryh
	pop de

	ld a, d
	cp 9 ; jump if a <= 8
	jr c, .smaller_than_9
		ld a, 8
	.smaller_than_9

	jp print_binaryh
; print_binary
; _print_binary


; _print_hexadecimalh
; 
; description:
; print the hexadecimal representation of a 8 bit value with precision
; 
; c function signiature:
; int print_hexadecimalh(unsigned char precision, unsigned char hexadecimal);
;
; input:
; - `a` : precision
; - `e` : hexadecimal
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_hexadecimalh:
	; fallthrough

; print_hexadecimalh
;
; input:
; - `a` : precision
; - `e` : hexadecimal
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_hexadecimalh:
	cp 3
	jr nc, .extra_padding
	ld d, a

	.print_hex
		ld a, e
		and $f0
		jr nz, .high_nibble_not_zero
			ld a, d
			cp 2
			jr c, .no_high_nibble
			xor a
		.high_nibble_not_zero
		swap a
		ld l, a
		ld h, high(hex_map)
		ld a, [hl]

		push de
		call putchar
		pop de

		.no_high_nibble
		ld a, e
		and $0f
		jr nz, .low_nibble_not_zero
			cp d
			ret z
		.low_nibble_not_zero
		ld l, a
		ld h, high(hex_map)
		ld a, [hl]
		jp putchar

	.extra_padding
		push de

		sub 2
		ld l, a
		ld h, "0" ; TODO: sign extend?

		.extra_padding_loop
			ld a, h
			push hl
			call putchar
			pop hl
			dec l
			jr nz, .extra_padding_loop

		pop de
		ld d, 2
		jr .print_hex
; print_hexadecimalh
; _print_hexadecimalh


; _print_hexadecimal
; 
; description:
; print the hexadecimal representation of a 16 bit value with precision
; 
; c function signiature:
; int print_hexadecimal(unsigned char precision, unsigned int hexadecimal);
;
; input:
; - `a`  : precision
; - `de` : hexadecimal
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_hexadecimal:
	; fallthrough

; print_hexadecimal
;
; input:
; - `a`  : precision TODO: 16 bit
; - `de` : hexadecimal
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_hexadecimal:
	cp 5
	jr nc, .extra_padding 

	.print_hex

	ld l, d
	ld d, a
	push de
	ld e, l
	
	sub 2 ; jump if a > 2
	jr nc, .bigger_than_2
		xor a ; ld a, 0
	.bigger_than_2

	call print_hexadecimalh
	pop de

	ld a, d
	cp 3 ; jump if a <= 2
	jr c, .smaller_than_3
		ld a, 2
	.smaller_than_3

	jp print_hexadecimalh

	.extra_padding
		push de
		sub 4
		ld l, a
		ld h, "0" ; TODO: sign extend?

		.extra_padding_loop
			push hl
			ld a, h
			call putchar
			pop hl
			dec l
			jr nz, .extra_padding_loop

		ld a, 4
		pop de
		jr .print_hex
; print_hexadecimal
; _print_hexadecimal


; _print_hexadecimall
; 
; description:
; print the hexadecimal representation of a 32 bit value with precision
; 
; c function signiature:
; int print_hexadecimall(unsigned char precision, unsigned long hexadecimal);
;
; input:
; - `a`    : precision
; - `debc` : hexadecimal
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_hexadecimall:
	; fallthrough

; print_hexadecimall
;
; input:
; - `a`    : precision TODO: 16 bit
; - `debc` : hexadecimal
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_hexadecimall:
	cp 9
	jr nc, .extra_padding 

	.print_hex

	push af
	push bc
	
	sub 4 ; jump if a > 4
	jr nc, .bigger_than_4
		xor a ; ld a, 0
	.bigger_than_4

	call print_hexadecimal
	pop de
	pop af

	cp 5 ; jump if a <= 4
	jr c, .smaller_than_5
		ld a, 4
	.smaller_than_5

	jp print_hexadecimal

	.extra_padding
		push de
		push bc
		sub 8
		ld l, a
		ld h, "0" ; TODO: sign extend?

		.extra_padding_loop
			push hl
			ld a, h
			call putchar
			pop hl
			dec l
			jr nz, .extra_padding_loop

		ld a, 8
		pop bc
		pop de
		jr .print_hex
; print_hexadecimall
; _print_hexadecimall


; _print_hex
; 
; description:
; print a hexadecimal integer,
; leading zero nibbles are ignored
;
; c function signiature:
; int print_hex(unsigned int);
;
; input:
; - `de` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_hex:
	; fallthrough

; print_hex
;
; input:
; - `de` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_hex:
	ld a, d
	or a
	jr z, .ignore_upper_bits
		push de
		call print_non_zero_nibbles
		pop de

		ld a, e
		call print_byte

		; TODO: return correct number of characters written
		ld bc, 3 ; or 4
		ret
	.ignore_upper_bits

	ld a, e
	call print_non_zero_nibbles

	; TODO: return correct number of characters written
	ld bc, 1 ; or 2
	ret
; print_hex
; _print_hex


; _print_hexl
; 
; description:
; print a hexadecimal long integer,
; leading zero nibbles are ignored
;
; c function signiature:
; int print_hexl(unsigned long);
;
; input:
; - `debc` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_hexl:
	; fallthrough

; print_hexl
;
; input:
; - `debc` : value
;
; returns:
; - `bc` : number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_hexl:
	ld a, d
	or e
	jr z, .ignore_upper_bits
		push bc
		call print_hex
		pop de

		call print_hex

		; TODO: return correct number of characters written
		ld bc, 5 ; to 8
		ret
	.ignore_upper_bits

	ld d, b
	ld e, c
	jp print_hex
; print_hexl
; _print_hexl


; _print_string
;
; c function signiature:
; int print_string(const char*);
;
; input:
; - `de` : string ptr
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_print_string:
	LD_R16_R16 hl, de
	; fallthrough

; print_string
;
; input:
; - `hl` : string ptr
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
print_string:
	ld a, [hli]
	or a
	ret z

	push hl
	call putchar
	pop hl
	jr print_string
; print_string
; _print_string


; _vprintf
;
; c function signiature:
; int vprintf(const char*, va_list);
;
; input:
; - `de` : fmt string ptr
; - `bc` : variable arguments list ptr
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_vprintf:
	LD_R16_R16 hl, de

; vprintf
;
; input:
; - `hl` : fmt string ptr
; - `bc` : variable arguments list ptr
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
vprintf:
	push bc

	.print_loop
		ld a, [hli]

		or a ; cp "\0"
		jr z, .exit
		
		cp "%"
		jr z, .fmt
		.return_from_fmt

		push hl
		call putchar
		pop hl
		jr .print_loop
	
	.exit
		pop bc
		ret
	
	.fmt
		ld d, 0 ; format flags
		pop bc

	.fmt_loop
		ld a, [hli]

		or a ; cp "\0"
		ret z

		; introductory % character
		; behaviour flags
		; minimum field width specfier
		; . precision specifier
		; length modifier
		; conversion format specifier

		cp "-"
		jp z, .flag_left_justified
		cp "+"
		jp z, .flag_sign_prepended
		cp " "
		jp z, .flag_space
		cp "#"
		jp z, .flag_alternative_form

		sub "0"
		jp z, .flag_leading_zeros
		cp 10
		jr c, .minimum_field_width_specifier 
		add "0"

		cp "."
		jp z, .precision_specifier
		.exit_precision_specifier

		; length modifiers: hh, h, l, ll, j, z, t, L

		cp "c"
		jp z, .conversion_c
		cp "s"
		jp z, .conversion_s
		cp "d"
		jp z, .conversion_i
		cp "i"
		jp z, .conversion_i
		cp "o"
		jp z, .conversion_o
		cp "x"
		jp z, .conversion_x_lower
		cp "X"
		jp z, .conversion_x_upper
		cp "u"
		jp z, .conversion_u
		cp "b"
		jp z, .conversion_b
		; f/F, e/E, a/A, g/G, n
		cp "p"
		jp z, .conversion_p

		.exit_fmt
		push bc
		jr .return_from_fmt

	.flag_left_justified
		set FORMATB_BEHAVIOUR_LEFT_JUSTIFIED, d
		jp .fmt_loop
	.flag_sign_prepended
		set FORMATB_BEHAVIOUR_SIGN_PREPENDED, d
		jp .fmt_loop
	.flag_space
		set FORMATB_BEHAVIOUR_SPACE, d
		jp .fmt_loop
	.flag_alternative_form
		set FORMATB_BEHAVIOUR_ALTERNATIVE_FORM, d
		jp .fmt_loop
	.flag_leading_zeros
		set FORMATB_BEHAVIOUR_LEADING_ZEROS, d
		jp .fmt_loop

	.minimum_field_width_specifier 
		; minimum field with specifier not supported yet
		set FORMATB_HAS_MINIMUM_FIELD_WIDTH_SPECIFIER, d
		jp .fmt_loop

	.precision_specifier
		bit FORMATB_HAS_PRECISION_SPECIFIER, d
		jr nz, .exit_fmt
		set FORMATB_HAS_PRECISION_SPECIFIER, d
		ld e, 0 ; precision
		
		ld a, [hli]

		or a ; cp "\0"
		ret z

		cp "*"
		jr z, .precision_specifier_asterisk
		sub "0"
		cp 10
		jr c, .precision_specifier_digit
		add "0"

		jp .exit_precision_specifier

	.precision_specifier_asterisk
		ld a, [bc]
		ld e, a
		inc bc
		inc bc

		ld a, [hli]

		or a ; cp "\0"
		ret z

		jp .exit_precision_specifier

	.precision_specifier_digit
		push bc

		; e = e * 10 + a
		ld b, a
		ld a, e
		ld c, a
		add a
		add a
		add c
		add a
		add b
		ld e, a

		pop bc

		ld a, [hli]

		or a ; cp "\0"
		ret z
		
		sub "0"
		cp 10
		jr c, .precision_specifier_digit
		add "0"
		jp .exit_precision_specifier

	.conversion_c
		push hl
		
		ld a, [bc]
		inc bc
		inc bc
		
		push bc
		call putchar
		pop bc

		pop hl
		jp vprintf

	.conversion_s
		push hl

		ld a, [bc]
		ld l, a
		inc bc
		ld a, [bc]
		ld h, a
		inc bc
		
		push bc
		call print_string
		pop bc

		pop hl
		jp vprintf

	.conversion_i
		push hl
		
		bit FORMATB_HAS_PRECISION_SPECIFIER, d
		jr nz, .i_has_precision_specifier
			ld e, 1
		.i_has_precision_specifier
		push de

		ld a, [bc]
		ld e, a
		inc bc
		ld a, [bc]
		ld d, a
		inc bc

		push bc

		cp $80
		jr c, .positive
			push de
			ld a, "-"
			call putchar
			pop de

			TWOS_COMPLEMENT_R16 de
		.positive

		call _bcd

		ld hl, sp+2
		ld a, [hl] ; precision
		call print_hexadecimall

		pop bc
		pop de

		pop hl
		jp vprintf 
	
	; TODO
	.conversion_o
		jp vprintf
	
	; TODO
	.conversion_x_lower
		; fallthrough
	.conversion_x_upper
		push hl
		
		bit FORMATB_HAS_PRECISION_SPECIFIER, d
		jr nz, .x_upper_has_precision_specifier
			ld e, 1
		.x_upper_has_precision_specifier
		ld l, e

		ld a, [bc]
		ld e, a
		inc bc
		ld a, [bc]
		ld d, a
		inc bc

		push bc

		ld a, l

		call print_hexadecimal

		pop bc

		pop hl
		jp vprintf
		
	.conversion_u
		push hl
		
		bit FORMATB_HAS_PRECISION_SPECIFIER, d
		jr nz, .u_has_precision_specifier
			ld e, 1
		.u_has_precision_specifier
		push de

		ld a, [bc]
		ld e, a
		inc bc
		ld a, [bc]
		ld d, a
		inc bc
		
		push bc

		call _bcd

		ld hl, sp+2
		ld a, [hl] ; precision
		call print_hexadecimall

		pop bc
		pop de

		pop hl
		jp vprintf

	.conversion_b
		push hl

		bit FORMATB_HAS_PRECISION_SPECIFIER, d
		jr nz, .b_has_precision_specifier
			ld e, 1
		.b_has_precision_specifier
		ld l, e

		ld a, [bc]
		ld e, a
		inc bc
		ld a, [bc]
		ld d, a
		inc bc

		push bc

		ld a, l

		call print_binary

		pop bc

		pop hl
		jp vprintf

	.conversion_p
		push hl

		ld a, [bc]
		ld e, a
		inc bc
		ld a, [bc]
		ld d, a
		inc bc

		push bc

		ld a, 4
		call print_hexadecimal

		pop bc

		pop hl
		jp vprintf
; vprintf
; _vprintf


; _printf
;
; c function signiature:
; int printf(const char*, ...);
;
; input:
; - stack : fmt string ptr, variable arguments
;
; returns:
; - `bc` : TODO: number of characters written
;
; destroys `af`, `bc`, `de`, `hl`
_printf:
	ld	hl, sp+4 ; variable arguments list ptr
	LD_R16_R16 bc, hl
	
	ld	hl, sp+2 ; format string ptr ptr
	ld	e, [hl]
	inc	hl
	ld	d, [hl]
	jp	_vprintf
; _printf
